group 'com.mapbox.maps.mapbox_maps'
version '1.0.0'

buildscript {
    ext.kotlin_version = '1.8.22'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    def token = System.getenv("SDK_REGISTRY_TOKEN") ?: project.findProperty('SDK_REGISTRY_TOKEN') ?: System.getenv("MAPBOX_DOWNLOADS_TOKEN") ?: project.findProperty('MAPBOX_DOWNLOADS_TOKEN')

    if (token == null || token.empty) {
        throw new Exception("SDK Registry token is null. See README.md for more information.")
    }
    repositories {
        google()
        mavenCentral()
        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = "mapbox"
                password = token
            }
        }
        maven {
            url 'https://api.mapbox.com/downloads/v2/snapshots/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = "mapbox"
                password = token
            }
        }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdk 33

    namespace 'com.mapbox.maps.mapbox_maps'
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    defaultConfig {
        minSdkVersion 21
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
    }
}

if (file("$rootDir/gradle/ktlint.gradle").exists() && file("$rootDir/gradle/lint.gradle").exists()) {
    project.apply {
        from("$rootDir/gradle/ktlint.gradle")
        from("$rootDir/gradle/lint.gradle")
    }
}

dependencies {
    implementation "androidx.annotation:annotation:1.5.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.2"
    implementation "com.google.android.gms:play-services-location:21.0.1"

    implementation "com.mapbox.common:common-ndk27:24.8.0"
    implementation "com.mapbox.maps:android-core-ndk27:11.8.0"
    implementation "com.mapbox.maps:android-ndk27:11.8.0"
    implementation ("com.mapbox.navigationcore:navigation:3.5.0") {
        exclude group: "com.mapbox.common", module: "common"
        exclude group: "com.mapbox.maps", module: "android"
        exclude group: "com.mapbox.maps", module: "base"
        exclude group: "com.mapbox.maps", module: "android-core"
    }
    implementation ("com.mapbox.navigationcore:ui-components:3.5.0") {
        exclude group: "com.mapbox.common", module: "common"
        exclude group: "com.mapbox.maps", module: "android"
        exclude group: "com.mapbox.maps", module: "base"
        exclude group: "com.mapbox.maps", module: "android-core"
    }

    configurations.all {
        resolutionStrategy {
            force "com.mapbox.maps:android-ndk27:11.8.0"
            force "com.mapbox.maps:base-ndk27:11.8.0"
            force "com.mapbox.common:common-ndk27:24.8.0"
            force "com.mapbox.maps:android-core-ndk27:11.8.0"
            force "com.mapbox.plugin:maps-locationcomponent-ndk27:11.8.0"
            force "com.mapbox.plugin:maps-lifecycle-ndk27:11.8.0"
            force "com.mapbox.plugin:maps-gestures-ndk27:11.8.0"
            force "com.mapbox.plugin:maps-viewport-ndk27:11.8.0"
            force "com.mapbox.module:maps-telemetry-ndk27:11.8.0"
        }
    }
}
